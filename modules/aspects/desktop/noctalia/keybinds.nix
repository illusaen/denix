{
  den.aspects.niri._.keybinds.hm =
    { config, lib, ... }:
    {
      programs.niri.settings.binds =
        let
          sh = config.lib.niri.actions.spawn "sh" "-c"; # Makeshift `spawn-sh` functionality
          # Noctalia IPC command runner
          noctalia = command: {
            action = sh "${lib.getExe config.programs.noctalia-shell.package} ipc call ${command}";
          };
          # Noctalia IPC command runner available on lock screen
          noctaliaWhileLocked =
            command:
            (noctalia command)
            // {
              allow-when-locked = true;
            };
        in
        lib.mapAttrs (_: value: lib.mkOverride 900 value) (
          lib.attrsets.mergeAttrsList [
            {
              # Launcher
              "Ctrl+Space" = noctalia "launcher toggle";
              "Mod+Shift+R" = noctalia "launcher command";
              "Mod+Ctrl+C" = noctalia "launcher clipboard";
            }
            {
              # Audio keys
              "XF86AudioMute" = noctaliaWhileLocked "volume muteOutput";
              "XF86AudioRaiseVolume" = noctaliaWhileLocked "volume increase";
              "XF86AudioLowerVolume" = noctaliaWhileLocked "volume decrease";
            }
            {
              # Plugins
              "Mod+Shift+Escape" = noctalia "plugin:keybind-cheatsheet toggle";
            }
          ]
        );
    };
}
